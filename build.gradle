apply plugin: 'java'

sourceCompatibility = '1.8'
[compileJava]*.options*.encoding = 'UTF-8'

import org.gradle.api.internal.file.copy.CopySpecInternal

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:6.0.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.26.0'
    }
}

apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'com.github.ben-manes.versions'

project.ext {
    PNC_VERSION = '5.1.4'
    MXBUILD_VERSION = '8.9.0.5487'
    MODULE_NAME = 'PushNotifications'
}

def runtimeLibs = "$buildDir/runtime/bundles"
def monoPath = '/Library/Frameworks/Mono.framework/Versions/Current/Commands'

def userLibDir = "$projectDir/test/userlib"
def testProject = "$projectDir/test/PushNotfications.mpr"

configurations {
    tar
}

repositories {
    mavenCentral()
    ivy {
        url 'https://cdn.mendix.com/'
        layout 'pattern', {
            artifact '/[organisation]/[module]-[revision].[ext]'
        }
        metadataSources {
            artifact()
        }
    }
}

dependencies {
    compile group: 'commons-codec', name: 'commons-codec', version: '1.13'
    compile group: 'com.google.api-client', name: 'google-api-client', version: '1.30.4'
    compile group: 'io.netty', name: 'netty-tcnative', version: '2.0.26.Final'
    compile group: 'com.turo', name: 'pushy', version: '0.13.9'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28'

    compile('io.netty:netty-codec-http2') {
        version {
            strictly '4.1.53.Final'
        }
    }

    compile('io.netty:netty-handler-proxy') {
        version {
            strictly '4.1.53.Final'
        }
    }

    compile('io.netty:netty-resolver-dns') {
        version {
            strictly '4.1.53.Final'
        }
    }

    compile('io.netty:netty-all') {
        version {
            strictly '4.1.53.Final'
        }
    }
    tar "runtime:mxbuild:${project.MXBUILD_VERSION}@tar.gz"
}

task extractModule( type: Exec ) {
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        executable "$buildDir/modeler/mxutil.exe"
        args 'create-module-package', '--filter-required-libs', '--exclude-files=resources/.*', "--package-dir=${projectDir}/dist/${project.PNC_VERSION}/module", testProject, project.MODULE_NAME
    } else {
        executable "$monoPath/mono"
        args "$buildDir/modeler/mxutil.exe", 'create-module-package', '--filter-required-libs', '--exclude-files=resources/.*', "--package-dir=${projectDir}/dist/${project.PNC_VERSION}/module", testProject, project.MODULE_NAME
    }
}

extractModule.doFirst {
    mkdir "${projectDir}/dist/${project.PNC_VERSION}/module"
}

task copyToUserlib( type: Copy ) {
    into userLibDir
    from configurations.runtime
    eachFile { fileCopyDetails ->
        def requiredLibFlag = new File(destinationDir, "${fileCopyDetails.name}.${project.MODULE_NAME}.RequiredLib")
        requiredLibFlag.write ''
    }
}

task untarMxbuild( type: Copy ) {
    configurations.tar.findAll{it.name.endsWith('tar.gz')}.each {
        from tarTree(resources.gzip(it))
        into buildDir
        include('**/modeler/**')
        includeEmptyDirs = false
    }
}

task prepareDeps {
    dependsOn 'clean', 'copyToUserlib', 'untarMxbuild'
}

clean {
    delete "$userLibDir"
}

tasks.untarMxbuild.shouldRunAfter tasks.copyToUserlib
